apply plugin: 'com.android.application'

def versionNamePrefix = '1.'

android {
    compileSdkVersion 26
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.android.smartlink.powertag"
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 2
        versionName getVersionName(versionNamePrefix)
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath true
                arguments = [eventBusIndex: 'com.android.smartlink.assist.eventbus.AppEventBusIndex']
            }
        }
    }

    signingConfigs {

        Properties properties = new Properties()
        InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
        properties.load(inputStream)

        release {
            storeFile file(properties.getProperty('STORE_FILE'))
            storePassword properties.getProperty('STORE_PASSWORD')
            keyAlias properties.getProperty('KEY_ALIAS')
            keyPassword properties.getProperty('KEY_PASSWORD')
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            android.applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    def type = productFlavors[0].name.contains("app") ? "phone" : "tablet"
                    outputFileName = "PowerTag_" + type + "_v" + "${getVersionName(versionNamePrefix)}.apk"
                }
            }
        }
    }

    flavorDimensions("product")

    productFlavors {
        app {
            applicationId "com.android.smartlink.powertag"
            dimension "product"
            resValue("string", "app_name", "PowerTag")
            manifestPlaceholders = [design_width_in_dp: "360", design_height_in_dp: "640"]
        }

        tablet {
            applicationId "com.android.smartlink.powertag.pad"
            dimension "product"
            resValue("string", "app_name", "PowerTag HD")
            manifestPlaceholders = [design_width_in_dp: "1280", design_height_in_dp: "800"]
        }
    }

    dataBinding {
        enabled = true
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false //lint 检测到layout-sw600dp独有文件会提示错误
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation(name: 'recyclerViewSupport_v1.0.1113', ext: 'aar')

    implementation "com.android.support:appcompat-v7:${SUPPORTLIBVERSION}"
    implementation "com.android.support:design:${SUPPORTLIBVERSION}"
    implementation "com.android.support:recyclerview-v7:${SUPPORTLIBVERSION}"
    implementation "com.android.support:cardview-v7:${SUPPORTLIBVERSION}"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.lzy.net:okgo:3.0.4'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.timehop.stickyheadersrecyclerview:library:0.4.3'
    implementation 'com.github.hackware1993:MagicIndicator:1.5.0'
    implementation 'com.umeng.analytics:analytics:6.1.4'

    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    implementation 'me.jessyan:autosize:1.0.5'
    implementation 'org.greenrobot:eventbus:3.1.1'
    annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.1.1'
}

static def getVersionName(String prefix) {
    return prefix + new Date().format("MMdd", TimeZone.getTimeZone("GMT+8"))
}