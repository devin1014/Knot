apply plugin: 'com.android.application'

android {

    compileSdkVersion 26
    buildToolsVersion '26.0.3'

    defaultConfig {
        applicationId "com.android.smartlink"
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath true
            }
        }
    }

    signingConfigs {
        release {
            storeFile file(project.hasProperty("store_file") ? store_file : RELEASE_STORE_FILE)
            storePassword(project.hasProperty('store_password') ? store_password : RELEASE_STORE_PASSWORD)
            keyAlias(project.hasProperty('key_alias') ? key_alias : RELEASE_KEY_ALIAS)
            keyPassword(project.hasProperty('key_password') ? key_password : RELEASE_KEY_PASSWORD)
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            android.applicationVariants.all { variant ->
                variant.outputs.all { output ->

                    def type = productFlavors[0].name.contains("app") ? "" : "_compat"

                    outputFileName = "chinaknot" + type + "_v" + "${releaseTime()}.apk"
                }
            }
        }
    }

    flavorDimensions("device")

    productFlavors {
        app {
            applicationId "com.android.smartlink"
            dimension "device"
            resValue("string", "app_name", "China Knot")
        }

        tabletCompat {
            applicationId "com.android.smartlink.pad"
            dimension "device"
            resValue("string", "app_name", "China Knot Display")
        }
    }

    dataBinding {
        enabled = true
    }
}

static def releaseTime() {
    return new Date().format("MMdd", TimeZone.getTimeZone("GMT+8"))
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile project(':core')

    compile(name: 'support_recyclerview_v1.0', ext: 'aar')

    compile("com.android.support:appcompat-v7:${SUPPORTLIBVERSION}")
    compile("com.android.support:design:${SUPPORTLIBVERSION}")
    compile("com.android.support:recyclerview-v7:${SUPPORTLIBVERSION}")
    compile("com.android.support:cardview-v7:${SUPPORTLIBVERSION}")
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.lzy.net:okgo:3.0.4'
    compile 'com.google.code.gson:gson:2.8.2'
    compile 'com.timehop.stickyheadersrecyclerview:library:0.4.3'

    compile 'com.umeng.analytics:analytics:6.1.2'

    compile 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    testCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

repositories {
    flatDir {
        dirs 'libs'
    }
}
